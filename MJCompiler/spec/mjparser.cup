

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal COLON, PLUS, RPAREN, COMMA, SEMICOLON, EXTENDS, BOOL, NEW, ELSE, IF, DOT, LCURLY;
terminal ASSIGN, MINUS, NOT_EQUAL, VOID, AND, LPAREN, EQUAL, LESS, LSQUARE;
terminal IS_EQUAL, BREAK, READ, INCREMENT, ASTERISK, GREATER, WHILE, SLASH;
terminal GREATER_EQUAL, LESS_EQUAL, CHAR, PRINT, FOREACH, RCURLY, CLASS, DECREMENT, PROG;
terminal RSQUARE, RETURN, CONTINUE, OR, CONST, PERCENT;
terminal Integer NUMBER;
terminal String IDENT;

nonterminal Program, Declaration, VarDecl, ConstDecl;
nonterminal Type;
nonterminal SingleConstDecl;
nonterminal MultipleConstDecl;
nonterminal SingleVarDecl;
nonterminal Brackets;
nonterminal MultipleVarDecl;

nonterminal ClassDecl;
nonterminal ClassConstrMethod;
nonterminal ClassMethod;
nonterminal ClassConstr;
nonterminal ClassVarDecl;
nonterminal ClassExtends;

nonterminal ConstructorDecl;
nonterminal ConstructorFormPars;
nonterminal ConstructorVarDecl;
nonterminal ConstructorStatement;

nonterminal MethodDecl;
nonterminal MethodFormPars;
nonterminal MethodStatement;
nonterminal MethodVarDecl;
nonterminal MultipleMethodDecl;

nonterminal FormPars;
nonterminal SingleFormPar;
nonterminal MultipleFormPar;

nonterminal Statement;
nonterminal StmtConst;
nonterminal ElseStatement;
nonterminal StatementList;

nonterminal Expr;
nonterminal Term;
nonterminal Factor;
nonterminal FactParam;

nonterminal DesignatorStatement;
nonterminal DesignArray;
nonterminal DesignOptional;
nonterminal ActPars;

nonterminal Condition;
nonterminal CondTerm;
nonterminal CondFact;

nonterminal Designator;

nonterminal Label;
nonterminal Assignop;
nonterminal Relop;
nonterminal Addop;
nonterminal Mulop;

nonterminal End;

precedence left ELSE;

Program ::= (Program) PROG IDENT Declaration LCURLY MultipleMethodDecl RCURLY;

Declaration ::= (ConstDeclaration) Declaration ConstDecl
			|
			(VarDeclaration) Declaration VarDecl
			|
			(ClassDeclaration) Declaration ClassDecl
			|
			(NoDeclaration) /* epsilon */
			;
		
ConstDecl ::= (ConstDecl) CONST Type:constType SingleConstDecl MultipleConstDecl SEMICOLON;

SingleConstDecl ::= (BoolConstDecl) IDENT:constName EQUAL BOOL:booleanConst
				|
				(IntegerConstDecl) IDENT:constName EQUAL NUMBER:numberConst
				|
				(CharConstDecl) IDENT:constName EQUAL CHAR:charConst
				;
				
MultipleConstDecl ::= (MultipleConstDecls) MultipleConstDecl COMMA SingleConstDecl
					|
					(EndOfDeclaration) /* epsilon */
					;					

VarDecl ::= (VarDecl) Type:varName SingleVarDecl MultipleVarDecl SEMICOLON;

SingleVarDecl ::= (SingleVarDecl) IDENT:varName Brackets;

Brackets ::= (Brackets) LSQUARE RSQUARE
			|
			(NoBrackets) /*epsilon*/
			;
			
MultipleVarDecl ::= (MultipleVarDecls) MultipleVarDecl COMMA SingleVarDecl
				|
				(EndOfVarDeclaration) /* epsilon */
				;
				

ClassDecl ::= (ClassDecl) CLASS IDENT ClassExtends LCURLY ClassVarDecl ClassConstrMethod RCURLY;

ClassConstrMethod ::= (ClassOnlyCurly) LCURLY RCURLY
					|
					(ClassConstrCurly) LCURLY ClassConstr RCURLY
					|
					(ClassMethodCurly) LCURLY ClassMethod RCURLY
					|
					(ClassConstrMethodCurly) LCURLY ClassConstr ClassMethod RCURLY
					|
					(NoClassConstrMethodCurly) /* epsilon */
					;
					
End ::= (End) /* epsilon */ ;

ClassMethod ::= (MultipleClassMethod) ClassMethod MethodDecl
			|
			(EndOfClassMethod) MethodDecl
			;
					
ClassConstr ::= (MultipleClassConstr) ClassConstr ConstructorDecl
				|
				(EndOfClassConstrDecl) ConstructorDecl
				;

ClassVarDecl ::= (MultipleClassVarDecl) ClassVarDecl VarDecl
				|
				(EndOfClassVarDecl) /* epsilon*/
				;

ClassExtends ::= (ClassExtend) EXTENDS Type
				|
				(NoClassExtend) /* epsilon */
				;

ConstructorDecl ::= (ConstructorDecl) IDENT LPAREN ConstructorFormPars RPAREN ConstructorVarDecl LCURLY ConstructorStatement RCURLY;

ConstructorFormPars ::= (ConstrFormParams) FormPars
				|
				(NoConstrFormParams) /* epsilon */
				;
				
ConstructorVarDecl ::= (ConstrVarDecls) MethodVarDecl VarDecl
				|
				(EndOfConstrVarDecl) /*epsilon*/
				;
				
ConstructorStatement ::= (ConstrMultipleStmt) ConstructorStatement Statement
					|
					(EndOfConstrStmt) /* epsilon */;


MultipleMethodDecl ::= (MethodDecls) MultipleMethodDecl MethodDecl
				|
				(NoMethodDeclList) /*epsilon*/
				;

MethodDecl ::= (TypeMethodDecl) Type:methodType IDENT:methodName LPAREN MethodFormPars RPAREN MethodVarDecl LCURLY MethodStatement RCURLY
					|
					(VoidMethodDecl) VOID IDENT:methodName LPAREN MethodFormPars RPAREN MethodVarDecl LCURLY MethodStatement RCURLY
					;
					
MethodFormPars ::= (MethodFormParams) FormPars
				|
				(NoMethodFormParams) /* epsilon */
				;
				
MethodVarDecl ::= (MethodVarDecls) MethodVarDecl VarDecl
				|
				(EndOfMethodVarDecl) /*epsilon*/
				;
				
MethodStatement ::= (MethodStmt) MethodStatement Statement
				|
				(NoMethodStmt) /* epsilon */
				;

FormPars ::= (FormPars) SingleFormPar MultipleFormPar;

SingleFormPar ::= (SingleFormParam) Type:formType IDENT:formName Brackets;

MultipleFormPar ::= (MultipleFormPars) MultipleFormPar COMMA SingleFormPar
				|
				(EndOfFormPar) /*epsilon*/
				;
				
Statement ::= (DesignStmt) DesignatorStatement SEMICOLON
			|
			(IfStmt) IF LPAREN Condition RPAREN Statement ElseStatement
			|
			(WhileStmt) WHILE LPAREN Condition RPAREN Statement
			|
			(BreakStmt) BREAK SEMICOLON
			|
			(ContinueStmt) CONTINUE SEMICOLON
			|
			(ReturnNoExpr) RETURN SEMICOLON
			|
			(ReturnExpr) RETURN Expr SEMICOLON
			|
			(ReadStmt) READ LPAREN Designator RPAREN SEMICOLON
			|
			(PrintStmt) PRINT LPAREN Expr StmtConst RPAREN SEMICOLON
			|
			(ForeachStmt) Designator DOT FOREACH LPAREN IDENT ASSIGN Statement RPAREN SEMICOLON
			|
			(ListStmts) LCURLY StatementList RCURLY
			;
			
StatementList ::= (MoreStatements) StatementList Statement
				|
				(EndOfStatements) /* epsilon */
				;
			
StmtConst ::= (StmtCnst) COMMA NUMBER
			|
			(NoStmtCnst) /* epsilon */
			;
			
ElseStatement ::= (ElseStmt) ELSE Statement
				|
				(NoElseStmt) /* epsilon */
				;

Expr ::= (NegativeExpr) MINUS Term
		|
		(MultipleExpr) Expr Addop Term
		|
		(SingleExpr) Term
		;
		
Term ::= (SingleTerm) Factor
		|
		(MultipleTerms) Term Mulop Factor
		;

Factor ::= (FactVar) Designator
		|
		(FactFunc) Designator LPAREN FactParam RPAREN 
		|
		(FactNum) NUMBER
		|
		(FactChar) CHAR
		|
		(FactBoolean) BOOL
		|
		(FactConstrArray) NEW Type:arrayType LSQUARE Expr RSQUARE
		|
		(FactConstrObject) NEW Type:objectType LPAREN FactParam RPAREN
		|
		(FactExpr) LPAREN Expr RPAREN
		;
		
FactParam ::= (FactFuncParams) ActPars
				|
				(NoFactFuncParams) /* epsilon */
				;
		
DesignatorStatement ::= (DesignAssign) Designator Assignop Expr
					|
					(DesignFunction) Designator LPAREN FactParam RPAREN 
					|
					(DesignInc) Designator INCREMENT
					|
					(DesignDec) Designator DECREMENT
					|
					(DesignAssignArray) LSQUARE DesignArray RSQUARE EQUAL Designator
					;
					
DesignArray ::= (MultipleDesignators) DesignArray COMMA DesignOptional 
			|
			(SingleDesignators) DesignOptional
			;
			
DesignOptional ::= (HasDesignator) Designator
				|
				(NoDesignator) /* epsilon */
				;
				
ActPars ::= (ActParams) ActPars COMMA Expr
		|
		(SingleActParam) Expr
		; 
		
Condition ::= (Conditions) CondTerm OR CondTerm
			|
			(SingleCondition) CondTerm
			;
				
CondTerm ::= (CondTerms) CondFact AND CondFact
			|
			(SingleCondTerm) CondFact
			;
				
CondFact ::= (CondFacts) CondFact Relop Expr
		|
		(SingleCondFact) Expr
		;
					
Designator ::= (MultipleDesignIdent) Designator DOT IDENT
			|
			(MultipleDesignExpr) Designator LSQUARE Expr RSQUARE
			|
			(SingleDesignIdent) IDENT;

Type ::= (Type) IDENT;	

Label ::= (Label) IDENT;

Assignop ::= (Assignop) EQUAL;

Relop ::= (IsEqual) IS_EQUAL
		|
		(NotEqual) NOT_EQUAL
		|
		(Greater) GREATER
		|
		(GreaterEqual) GREATER_EQUAL
		|
		(Less) LESS
		|
		(LessEqual) LESS_EQUAL
		;

Addop ::= (Plus) PLUS
		|
		(Minus) MINUS
		;

Mulop ::= (Asterisk) ASTERISK
		|
		(Slash) SLASH
		|
		(Percent) PERCENT
		;


